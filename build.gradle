plugins {
    id "java-gradle-plugin"
    id "maven-publish"
    id "jacoco"
    id "com.gradle.plugin-publish" version "0.21.0"
    id 'org.ajoberstar.reckon' version "0.13.2"
    id "org.sonarqube" version "4.0.0.2929"
    id "be.vbgn.ci-detect" version "0.5.0"
    id "be.vbgn.dev-conventions.opinion" version "0.5.3"
}
group "be.vbgn.gradle"

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation localGroovy()

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation gradleTestKit()
    testImplementation "junit:junit:4.13.2"
}

gradlePlugin {
    plugins {
        ciDetect {
            id = "be.vbgn.ci-detect"
            implementationClass = "be.vbgn.gradle.cidetect.CiDetectPlugin"
        }
    }
}

pluginBundle {
    vcsUrl = "https://github.com/vierbergenlars/ci-detect-gradle-plugin"
    website = vcsUrl
    description = "Plugin that checks if it is running on a continuous integration server and gives you access to the build metadata."
    tags = ["continuous-integration", "ci", "jenkins", "travis-ci", "gitlab-ci", "github-actions"]
    plugins {
        ciDetect {
            displayName = "CI detect plugin"
        }
    }
}

test {
    forkEvery = 1
}
